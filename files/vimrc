
set nocompatible        " Must be first line

" ======================================================================== "
"                            Platform settings                             "
" ======================================================================== "

silent func! OSX()
    return has('macunix')
endfunc
silent func! LINUX()
    return has('unix') && !has('macunix') && !has('win32unix')
endfunc
silent func! WINDOWS()
    return has('win16') || has('win32') || has('win64')
endfunc

" On Windows, also use '.vim' instead of 'vimfiles'; this makes synchronization
" across (heterogeneous) systems easier.
if WINDOWS()
    set rtp=$HOME/.vim,$VIM/vimfiles,$VIMRUNTIME,$VIM/vimfiles/after,$HOME/.vim/after
    " Be nice and check for multi_byte even if the config requires
    " multi_byte support most of the time
    if has("multi_byte")
        " Windows cmd.exe still uses cp850. If Windows ever moved to
        " Powershell as the primary terminal, this would be utf-8
        set termencoding=cp850
        " Let Vim use utf-8 internally, because many scripts require this
        set encoding=utf-8
        setglobal fileencoding=utf-8
    endif
endif

" ======================================================================== "
"                          Setup Vundle & Plugins                          "
" ======================================================================== "

filetype off
set rtp+=~/.vim/bundle/vundle
call vundle#begin()

Plugin 'gmarik/vundle'

" Chinese documentation
Plugin 'asins/vimcdoc'

" Additional
Plugin 'tomtom/tlib_vim'
Plugin 'mattn/webapi-vim'
Plugin 'Shougo/vimproc.vim'
Plugin 'MarcWeber/vim-addon-mw-utils'

if executable('ag')
    let g:ackprg = 'ag --nogroup --nocolor --column --smart-case'
    Plugin 'mileszs/ack.vim'
elseif executable('ack-grep')
    let g:ackprg = "ack-grep -H --nocolor --nogroup --column"
    Plugin 'mileszs/ack.vim'
elseif executable('ack')
    Plugin 'mileszs/ack.vim'
endif

" General

" ColorSchemes
Plugin 'altercation/vim-colors-solarized'
" Plugin 'flazz/vim-colorschemes'

" Indicators
Plugin 'bling/vim-airline'
Plugin 'nathanaelkane/vim-indent-guides'
Plugin 'kshenoy/vim-signature'

" Navigators
Plugin 'mbbill/undotree'
Plugin 'scrooloose/nerdtree'
Plugin 'jistr/vim-nerdtree-tabs'
Plugin 'dyng/ctrlsf.vim'
Plugin 'ctrlpvim/ctrlp.vim'
Plugin 'tacahiroy/ctrlp-funky'

" SessionManagers
Plugin 'sessionman.vim'
Plugin 'restore_view.vim'

" Utils
Plugin 'szw/vim-g'
Plugin 'qpkorr/vim-renamer'
Plugin 'lilydjwg/fcitx.vim'
Plugin 'gioele/vim-autoswap'
Plugin 'vim-scripts/sudo.vim'

" Textobjs
Plugin 'wellle/targets.vim'
Plugin 'kana/vim-textobj-user'
Plugin 'kana/vim-textobj-line'
Plugin 'bps/vim-textobj-python'
Plugin 'kana/vim-textobj-indent'
Plugin 'kana/vim-textobj-entire'
Plugin 'reedes/vim-textobj-quote'
Plugin 'reedes/vim-textobj-sentence'
Plugin 'gaving/vim-textobj-argument'

" Editing
Plugin 'vis'
Plugin 'DrawIt'
Plugin 'VisIncr'
Plugin 'tpope/vim-repeat'
Plugin 'gcmt/wildfire.vim'
Plugin 'tpope/vim-surround'
Plugin 'spf13/vim-autoclose'
Plugin 'osyo-manga/vim-over'
Plugin 'Lokaltog/vim-easymotion'
Plugin 'kristijanhusak/vim-multiple-cursors'

" Writing
Plugin 'reedes/vim-wordy'
Plugin 'tpope/vim-abolish'
Plugin 'reedes/vim-litecorrect'

" Coding
Plugin 'godlygeek/tabular'
Plugin 'tomtom/tcomment_vim'
Plugin 'scrooloose/syntastic'
Plugin 'aperezdc/vim-template'
Plugin 'Chiel92/vim-autoformat'
Plugin 'johnsyweb/vim-makeshift'
if executable('ctags')
    Plugin 'majutsushi/tagbar'
endif

" Git
Plugin 'mattn/gist-vim'
Plugin 'mhinz/vim-signify'
Plugin 'tpope/vim-fugitive'
Plugin 'Xuyuanp/nerdtree-git-plugin'

" AutoCompletion
let s:completer = 'youcompleteme'

if s:completer == 'snipmate'
    Plugin 'garbas/vim-snipmate'
    Plugin 'honza/vim-snippets'
    " Source support_function.vim to support vim-snippets.
    if filereadable(expand("~/.vim/bundle/vim-snippets/snippets/support_functions.vim"))
        source ~/.vim/bundle/vim-snippets/snippets/support_functions.vim
    endif
elseif s:completer == 'youcompleteme'
    Plugin 'Valloric/YouCompleteMe'
    Plugin 'SirVer/ultisnips'
    Plugin 'honza/vim-snippets'
elseif s:completer == 'neocomplcache'
    Plugin 'Shougo/neocomplcache.vim'
    Plugin 'Shougo/neosnippet'
    Plugin 'Shougo/neosnippet-snippets'
    Plugin 'honza/vim-snippets'
elseif s:completer == 'neocomplete'
    Plugin 'Shougo/neocomplete.vim'
    Plugin 'Shougo/neosnippet'
    Plugin 'Shougo/neosnippet-snippets'
    Plugin 'honza/vim-snippets'
endif

" Languages

"" Markdown
Plugin 'tpope/vim-markdown'
Plugin 'suan/vim-instant-markdown'

"" Python
Plugin 'python_match.vim'
Plugin 'klen/python-mode'
Plugin 'jmcantrell/vim-virtualenv'
Plugin 'mjbrownie/vim-htmldjango_omnicomplete'

"" HTML
Plugin 'tpope/vim-haml'
Plugin 'mattn/emmet-vim'
Plugin 'amirh/HTML-AutoCloseTag'

"" CSS
Plugin 'groenewege/vim-less'
Plugin 'hail2u/vim-css3-syntax'

"" JavaScript
Plugin 'elzr/vim-json'
Plugin 'briancollins/vim-jst'
Plugin 'pangloss/vim-javascript'
Plugin 'kchmck/vim-coffee-script'

"" Haskell
" Plugin 'dag/vim2hs'
" Plugin 'travitch/hasksyn'
" Plugin 'eagletmt/neco-ghc'
" Plugin 'bitc/vim-hdevtools'
" Plugin 'eagletmt/ghcmod-vim'
" Plugin 'Twinside/vim-haskellFold'
" Plugin 'Twinside/vim-haskellConceal'
Plugin 'lukerandall/haskellmode-vim'

"" Kotlin
Plugin 'udalov/kotlin-vim'

"" elm
Plugin 'lambdatoast/elm.vim'

"" Configuration files
Plugin 'PotatoesMaster/i3-vim-syntax'
Plugin 'Matt-Deacalion/vim-systemd-syntax'

call vundle#end()

" Add an UnBundle command
func! UnBundle(arg, ...)
    let bundle = vundle#config#init_bundle(a:arg, a:000)
    call fiiter(g:vundle#bundles, 'v:val["name_spec"] != "' . a:arg . '"')
endfunc

com! -nargs=+ UnBundle call UnBundle(<args>)

" ======================================================================== "
"                            Personal Settings                             "
" ======================================================================== "

let s:your_name = 'zlsun'
let s:your_email = 'zlsun1995@gmail.com'
let s:your_default_license = 'MIT'

" ======================================================================== "
"                             General Settings                             "
" ======================================================================== "

set fileencodings=ucs-bom,utf-8,cp936,gb18030,big5,euc-jp,euc-kr,latin1
set sessionoptions+=globals,localoptions,resize,winpos
filetype plugin indent on   " Automatically detect file types.
syntax on                   " Syntax highlighting
scriptencoding utf-8

if has('clipboard')
    if has('unnamedplus')  " When possible use + register for copy-paste
        set clipboard=unnamed,unnamedplus
    else         " On mac and Windows, use * register for copy-paste
        set clipboard=unnamed
    endif
endif

set timeoutlen=500 ttimeoutlen=0
set shortmess+=filmnrxoOtT                      " Abbrev. of messages (avoids 'hit enter')
set viewoptions=folds,options,cursor,unix,slash " Better Unix / Windows compatibility
set virtualedit=block,onemore                   " Allow for cursor beyond last character
set history=1000                                " Store a ton of history (default is 20)
set nospell                                     " Spell checking off
set hidden                                      " Allow buffer switching without saving
set iskeyword-=.                                " '.' is an end of word designator
set iskeyword-=#                                " '#' is an end of word designator
set iskeyword-=-                                " '-' is an end of word designator

set backspace=indent,eol,start  " Backspace for dummies
set showmatch                   " Show matching brackets/parenthesis
set incsearch                   " Find as you type search
set hlsearch                    " Highlight search terms
set winminheight=0              " Windows can be 0 line high
set ignorecase                  " Case insensitive search
set smartcase                   " Case sensitive when uc present
set wildmenu                    " Show list instead of just completing
set wildmode=list:longest,full  " Command <Tab> completion, list matches, then longest common part, then all.
set whichwrap=b,s,h,l,<,>,[,]   " Backspace and cursor keys wrap too
set scrolljump=5                " Lines to scroll when cursor leaves screen
set scrolloff=3                 " Minimum lines to keep above and below cursor
set foldenable                  " Auto fold code

set nowrap                      " Do not wrap long lines
set autoindent                  " Indent at the same level of the previous line
set autoread                    " Automatically read file when changed
set shiftwidth=4                " Use indents of 4 spaces
set expandtab                   " Tabs are spaces, not tabs
set tabstop=4                   " An indentation every four columns
set softtabstop=4               " Let backspace delete indent
set nojoinspaces                " Prevents inserting two spaces after punctuation on a join (J)
set splitright                  " Puts new vsplit windows to the right of the current
set splitbelow                  " Puts new split windows to the bottom of the current
set pastetoggle=<F12>           " pastetoggle (sane indentation on pastes)

" Setting up the directories
set backup                      " Backups are nice ...
if has('persistent_undo')
    set undofile                " So is persistent undo ...
    set undolevels=1000         " Maximum number of changes that can be undone
    set undoreload=10000        " Maximum number lines to save for undo on a buffer reload
endif

set tags=./tags;/,~/.vimtags

" Make tags placed in .git/tags file available in all levels of a repository
let gitroot = substitute(system('git rev-parse --show-toplevel'), '[\n\r]', '', 'g')
if gitroot != ''
    let &tags = &tags . ',' . gitroot . '/.git/tags'
endif

" Add exclusions to mkview and loadview
" eg: *.*, svn-commit.tmp
let g:skipview_files = [
    \ '\[example pattern\]'
    \ ]

let g:netrw_browsex_viewer =  "x-www-browser"

" ======================================================================== "
"                               UI Settings                                "
" ======================================================================== "

set background=dark         " Assume a dark background
set colorcolumn=80,100      " Highlight column 80 and 100
set cursorline              " Highlight current line
set linespace=0             " No extra spaces between rows
set mouse=a                 " Automatically enable mouse usage
set mousehide               " Hide the mouse cursor while typing
set noeb novb t_vb=         " Disable beeping
set number                  " Line numbers on
set relativenumber          " Enable relative line number
set showmode                " Display the current mode
set tabpagemax=15           " Only show 15 tabs

" Custom invisibles
set list
set listchars=tab:›\ ,trail:•,extends:❯,precedes:❮,nbsp:.
set showbreak=↪\ \ 

if has('cmdline_info')
    set ruler   " Show the ruler
    set rulerformat=%30(%=\:b%n%y%m%r%w\ %l,%c%V\ %P%) " A ruler on steroids
    set showcmd " Show partial commands in status line and selected characters/lines in visual mode
endif

if has('statusline')
    set laststatus=2
    " Broken down into easily includeable segments
    set statusline=%<%f\                     " Filename
    set statusline+=%w%h%m%r                 " Options
    set statusline+=\ [%{&ff}/%Y]            " Filetype
    set statusline+=\ [%{getcwd()}]          " Current dir
    set statusline+=%=%-14.(%l,%c%V%)\ %p%%  " Right aligned file nav info
endif

" GVIM- (here instead of .gvimrc)
if has('gui_running')
    set lines=40                " 40 lines of text instead of 24
    set guioptions=ai
    set linespace=-2
    if LINUX()
        set guifont=monospace\ 11
    elseif OSX()
        set guifont=Andale\ Mono\ Regular:h12,Menlo\ Regular:h11,Consolas\ Regular:h12,Courier\ New\ Regular:h14
    elseif WINDOWS()
        set guifont=Andale_Mono:h10,Menlo:h10,Consolas:h11,Courier_New:h10
    endif
else
    if &term == 'xterm' || &term == 'screen'
        set t_Co=256 " Enable 256 colors to stop the CSApprox warning and make xterm vim shine
    endif
    if &term =~ '^screen'
        " tmux will send xterm-style keys when its xterm-keys option is on
        execute "set <xUp>=\e[1;*A"
        execute "set <xDown>=\e[1;*B"
        execute "set <xRight>=\e[1;*C"
        execute "set <xLeft>=\e[1;*D"
    endif
endif

" ======================================================================== "
"                                Functions                                 "
" ======================================================================== "

func! InitializeDirectories()
    let parent = $HOME
    let prefix = ''
    let dir_list = {
                \ 'backup': 'backupdir',
                \ 'views': 'viewdir',
                \ 'swap': 'directory',
                \ }

    if has('persistent_undo')
        let dir_list['undo'] = 'undodir'
    endif

    if exists('g:consolidated_directory')
        let common_dir = g:consolidated_directory . prefix
    else
        let common_dir = parent . '/.' . prefix
    endif

    for [dirname, settingname] in items(dir_list)
        let directory = common_dir . dirname . '/'
        if exists("*mkdir")
            if !isdirectory(directory)
                call mkdir(directory, "p")
            endif
        endif
        if !isdirectory(directory)
            echo "Warning: Unable to create backup directory: " . directory
            echo "Try: mkdir -p " . directory
        else
            let directory = substitute(directory, " ", "\\\\ ", "g")
            exec "set " . settingname . "=" . directory
        endif
    endfor

    let viminfo = common_dir . 'info'
    exec "set viminfo+=n" . viminfo

endfunc

let g:consolidated_directory = $HOME . '/.vim/'
call InitializeDirectories()


func! NERDTreeInitAsNeeded()
    redir => bufoutput
    buffers!
    redir END
    let idx = stridx(bufoutput, "NERD_tree")
    if idx > -1
        NERDTreeMirror
        NERDTreeFind
        wincmd l
    endif
endfunc


func! StripTrailingWhitespace()
    " Preparation: save last search, and cursor position.
    let _s = @/
    let l = line(".")
    let c = col(".")
    " do the business:
    %s/\s\+$//e
    " clean up: restore previous search history, and cursor position
    let @/ = _s
    call cursor(l, c)
endfunc


func! s:RunShellCommand(cmdline)
    botright new

    setlocal buftype=nofile
    setlocal bufhidden=delete
    setlocal nobuflisted
    setlocal noswapfile
    setlocal nowrap
    setlocal filetype=shell
    setlocal syntax=shell

    call setline(1, a:cmdline)
    call setline(2, substitute(a:cmdline, '.', '=', 'g'))
    execute 'silent $read !' . escape(a:cmdline, '%#')
    setlocal nomodifiable
    1
endfunc

com! -complete=file -nargs=+ Shell call s:RunShellCommand(<q-args>)
" e.g. Grep current file for <search_term>: Shell grep -Hn <search_term> %

" ======================================================================== "
"                               Autocommands                               "
" ======================================================================== "

" Most prefer to automatically switch to the current file directory when a new
" buffer is opened.
au BufEnter * if bufname("") !~ "^\[A-Za-z0-9\]*://|sudo:"
           \|     lcd %:p:h
           \| endif

" Instead of reverting the cursor to the last position in the buffer, we
" set it to the first line when editing a git commit message
au FileType gitcommit au! BufEnter COMMIT_EDITMSG call setpos('.', [0, 1, 1, 0])

" http://vim.wikia.com/wiki/Restore_cursor_to_file_position_in_previous_editing_session
" Restore cursor to file position in previous editing session
func! ResCur()
    if line("'\"") <= line("$")
        normal! g`"
        return 1
    endif
endfunc

aug resCur
    au!
    au BufWinEnter * call ResCur()
aug END

" Set scripts to be executable from the shell
func! Chmod(file)
    if getline(1) =~ "^#!.*/bin/"
        silent !chmod +x a:file
    endif
endfunc
" au BufWritePost * call Chmod(<afile>)

aug source vimrc
    au!
    au FileWritePost .vimrc source %
    au FileWritePost vimrc source %
aug END

" Remove trailing whitespaces and ^M chars
au FileType c,cpp,elm,go,haskell,java,javascript,nasm,perl,php,python,ruby,sh,yml
    \ au BufWritePre <buffer> call StripTrailingWhitespace()

au FileType txt set wrap

au FileType haskell,puppet,ruby,yml setlocal expandtab shiftwidth=2 softtabstop=2
" preceding line best in a plugin but here for now.
" Workaround vim-commentary for Haskell
au FileType haskell setlocal commentstring=--\ %s
" Workaround broken color highlighting in Haskell
au FileType haskell,rust setlocal nospell

" Set filetypes
au BufNewFile,BufRead *.html.twig set filetype=html.twig
au BufNewFile,BufRead *.coffee    set filetype=coffee
au BufNewFile,BufRead *.asm       set filetype=nasm
au BufNewFile,BufRead *.inc       set filetype=nasm
au BufNewFile,BufRead *.zsh-theme set filetype=zsh

" ======================================================================== "
"                               Key mappings                               "
" ======================================================================== "

" Shortcuts
" Change Working Directory to that of the current file
cmap cwd lcd %:p:h
cmap cd. lcd %:p:h

" For when you forget to sudo.. Really Write the file.
cmap w!! w !sudo tee % >/dev/null

" Stupid shift key fixes
if has("user_commands")
    com! -bang -nargs=* -complete=file E   e<bang> <args>
    com! -bang -nargs=* -complete=file W   w<bang> <args>
    com! -bang -nargs=* -complete=file Wq wq<bang> <args>
    com! -bang -nargs=* -complete=file WQ wq<bang> <args>
    com! -bang Wa wa<bang>
    com! -bang WA wa<bang>
    com! -bang Q   q<bang>
    com! -bang QA qa<bang>
    com! -bang Qa qa<bang>
endif

cmap Tabe tabe

" The default leader is '\', but many people prefer ',' as it's in a standard
" location.
let mapleader = ','
let maplocalleader = '_'

" Map space as leader.
" see: http://redd.it/1vdrxg
nmap <Space> <Leader>
nmap <Space><Space> <Leader><Leader>

" map ; to :
" see: http://vim.wikia.com/wiki/Map_semicolon_to_colon
map ; :
noremap ;; ;

" Wrapped lines goes down/up to next row, rather than next line in file.
noremap j gj
noremap k gk

" Smash Escape
inoremap jk <Esc>
inoremap kj <Esc>

" Duplicate selected text
vnoremap D y`>p`[v`]

" Yank from the cursor to the end of the line, to be consistent with C and D.
nnoremap Y y$

" Map Q to replace
nmap Q :%s///g<Left><Left><Left>
vmap Q :s///g<Left><Left><Left>

" Allow using the repeat operator with a visual selection (!)
" http://stackoverflow.com/a/8064607/127816
vnoremap . :normal .<CR>

" Easier horizontal scrolling
map zl zL
map zh zH

" http://vim.wikia.com/wiki/Selecting_your_pasted_text
nnoremap gp `[v`]

" Visual shifting (does not exit Visual mode)
vnoremap < <gv
vnoremap > >gv

" Fullscreen mode for GVIM and Terminal, need 'wmctrl' in you PATH
map <silent> <F11> :call system("wmctrl -ir " . v:windowid . " -b toggle,fullscreen")<CR>

" The following two lines conflict with moving to top and
" bottom of the screen
map <S-H> gT
map <S-L> gt

" move line up and down
nnoremap <A-j> :m .+1<CR>
nnoremap <A-k> :m .-2<CR>
inoremap <A-j> <Esc>:m .+1<CR>gi
inoremap <A-k> <Esc>:m .-2<CR>gi
vnoremap <A-j> :m '>+1<CR>gv
vnoremap <A-k> :m '<-2<CR>gv

" Easier moving in tabs and windows
" The lines conflict with the default digraph mapping of <C-K>
map <C-J> <C-W>j
map <C-K> <C-W>k
map <C-L> <C-W>l
map <C-H> <C-W>h

inoremap <C-Z> <Esc><C-Z>i

inoremap <C-CR> <C-O>o
inoremap <C-S-CR> <C-O>O

" Easier formatting
nnoremap <leader>q gwip

" Easier window operation
map <Leader>w <C-W>

" Adjust viewports to the same size
map <Leader>= <C-W>=

" Most prefer to toggle search highlighting rather than clear the current
" search results.
" nmap <silent> <leader>/ :nohlsearch<CR>
nmap <silent> <leader>/ :set invhlsearch<CR>

" Copy the whole file
map <Leader>cf :<C-U>%y+<CR>

" Find merge conflict markers
map <leader>fc /\v^[<\|=>]{7}( .*\|$)<CR>

" Display all lines with keyword under cursor and ask which one to jump to
nmap <Leader>ff [I:let nr = input("Which one: ")<Bar>exe "normal " . nr ."[\t"<CR>

" Code folding options
nmap <leader>f0 :set foldlevel=0<CR>
nmap <leader>f1 :set foldlevel=1<CR>
nmap <leader>f2 :set foldlevel=2<CR>
nmap <leader>f3 :set foldlevel=3<CR>
nmap <leader>f4 :set foldlevel=4<CR>
nmap <leader>f5 :set foldlevel=5<CR>
nmap <leader>f6 :set foldlevel=6<CR>
nmap <leader>f7 :set foldlevel=7<CR>
nmap <leader>f8 :set foldlevel=8<CR>
nmap <leader>f9 :set foldlevel=9<CR>

" Render selected text with Jinja2
vmap <Leader>rd :!envtpl<CR>

" Source current file
" map <Leader>so :source %<CR>

" Some helpers to edit mode
" http://vimcasts.org/e/14
cnoremap %% <C-R>=fnameescape(expand('%:h')).'/'<cr>
map <leader>ew :e %%
map <leader>es :sp %%
map <leader>ev :vsp %%
map <leader>et :tabe %%

" For tab
map <Leader>tn :tabnew<CR>
map <Leader>to :tabonly<CR>
map <Leader>tk :tabnext<CR>
map <Leader>tl :tablast<CR>
map <Leader>tc :tabclose<CR>
map <Leader>th :tabfirst<CR>
map <Leader>tj :tabprevious<CR>

map      <F5> :!./%<CR>
map      <F6> :<C-U>make!<CR>
map    <S-F6> :<C-U>make!<space>
map <Leader>b :<C-U>make!<CR>
map <Leader>B :<C-U>make!<space>

imap    <F5> <Esc>:!./%<CR>
imap    <F6> <Esc>:<C-U>make!<CR>
imap  <S-F6> <Esc>:<C-U>make!<space>
imap <C-S-B> <Esc>:<C-U>make!<space>
imap   <C-B> <Esc>:<C-U>make!<CR>

" If the current buffer has never been saved, it will have no name, call the
" file browser to save it, otherwise just save it. see:
" http://vim.wikia.com/wiki/Map_Ctrl-S_to_save_current_or_new_files
com! -nargs=0 -bar Update
    \  if &modified
    \|     if empty(bufname('%'))
    \|         browse confirm write
    \|     else
    \|         confirm write
    \|     endif
    \| endif
noremap <silent> <C-S> :<C-U>Update<CR>
inoremap <silent> <C-S> <C-O>:Update<CR>

" Spell Check
" see: http://vim.wikia.com/wiki/Toggle_spellcheck_with_function_keys

let g:spell_lang_index = 0
let g:spell_lang_list = ["nospell","en"]

func! ToggleSpell()
    let g:spell_lang_index = g:spell_lang_index + 1
    if g:spell_lang_index >= len(g:spell_lang_list)
        let g:spell_lang_index = 0
    endif
    if g:spell_lang_index == 0
        setlocal nospell
    else
        execute "setlocal spell spelllang=" . get(g:spell_lang_list, g:spell_lang_index)
    endif
    echo "spell checking language:" g:spell_lang_list[g:spell_lang_index]
endfunc

nmap <silent> <F7> :call ToggleSpell()<CR>
imap <silent> <F7> <C-O>: call ToggleSpell()<CR>

au BufEnter * let b:in_hex_view = 0
func! ToggleHexView()
    if b:in_hex_view
        %!xxd -r
    else
        %!xxd
    endif
    let b:in_hex_view = !b:in_hex_view
endfunc

nmap <silent> <F8> :call ToggleHexView()<CR>

" End/Start of line motion keys act relative to row/wrap width in the presence
" of `:set wrap`, and relative to line for `:set nowrap`. Default vim behaviour
" is to act relative to text line in both cases Same for 0, home, end, etc
func! WrapRelativeMotion(key, ...)
    let vis_sel = ""
    if a:0
        let vis_sel = "gv"
    endif
    if &wrap
        execute "normal!" vis_sel . "g" . a:key
    else
        execute "normal!" vis_sel . a:key
    endif
endfunc

" Map g* keys in Normal, Operator-pending, and Visual+select
noremap <silent> $       :call WrapRelativeMotion("$")<CR>
noremap <silent> <End>   :call WrapRelativeMotion("$")<CR>
noremap <silent> 0       :call WrapRelativeMotion("0")<CR>
noremap <silent> <Home>  :call WrapRelativeMotion("0")<CR>
noremap <silent> ^       :call WrapRelativeMotion("^")<CR>
" Overwrite the operator pending $/<End> mappings from above
" to force inclusive motion with :execute normal!
onoremap <silent> $     v:call WrapRelativeMotion("$")<CR>
onoremap <silent> <End> v:call WrapRelativeMotion("$")<CR>
" Overwrite the Visual+select mode mappings from above
" to ensure the correct vis_sel flag is passed to function
vnoremap <silent> $      :<C-U>call WrapRelativeMotion("$", 1)<CR>
vnoremap <silent> <End>  :<C-U>call WrapRelativeMotion("$", 1)<CR>
vnoremap <silent> 0      :<C-U>call WrapRelativeMotion("0", 1)<CR>
vnoremap <silent> <Home> :<C-U>call WrapRelativeMotion("0", 1)<CR>
vnoremap <silent> ^      :<C-U>call WrapRelativeMotion("^", 1)<CR>

" ======================================================================== "
"                                 Plugins                                  "
" ======================================================================== "

func! s:has_bundle(name)
    if !isdirectory(expand("~/.vim/bundle/" . a:name))
        echo a:name . " is not installed, run :VundleInstall to install"
        return 0
    endif
    return 1
endfunc

if s:has_bundle('ack.vim')
    cmap ack Ack
    let g:ackhighlight = 1
endif

if s:has_bundle('vim-colors-solarized')
    " let g:solarized_termcolors = 256
    let g:solarized_termtrans = 1
    let g:solarized_contrast = "normal"
    let g:solarized_visibility = "normal"
    color solarized              " Load a colorscheme
    hi MatchParen cterm=bold ctermbg=red ctermfg=white
    hi clear SignColumn   " SignColumn should match background
    hi clear CursorLineNr " Remove highlight color from current line number
endif

if s:has_bundle('vim-airline')
    let g:airline_powerline_fonts = 1
    let g:airline_theme = 'solarized'
    if !exists('g:airline_powerline_fonts')
        " Use the default set of separators with a few customizations
        let g:airline_left_sep = '›'  " Slightly fancier than '>'
        let g:airline_right_sep = '‹' " Slightly fancier than '<'
    endif
    let g:airline#extensions#virtualenv#enabled = 1
    let g:airline#extensions#tabline#enabled = 1
    let g:airline#extensions#tabline#buffer_idx_mode = 1
    if g:airline#extensions#tabline#enabled == 1
        nmap <leader>1 <Plug>AirlineSelectTab1
        nmap <leader>2 <Plug>AirlineSelectTab2
        nmap <leader>3 <Plug>AirlineSelectTab3
        nmap <leader>4 <Plug>AirlineSelectTab4
        nmap <leader>5 <Plug>AirlineSelectTab5
        nmap <leader>6 <Plug>AirlineSelectTab6
        nmap <leader>7 <Plug>AirlineSelectTab7
        nmap <leader>8 <Plug>AirlineSelectTab8
        nmap <leader>9 <Plug>AirlineSelectTab9
        nmap <leader>0 <Plug>AirlineSelectTab0
    endif
    let g:airline#extensions#whitespace#trailing_format = 'trail[%s]'
    let g:airline#extensions#whitespace#mixed_indent_format = 'mixed[%s]'
endif

if s:has_bundle('indentLine')
    " Vim
    let g:indentLine_color_term = 10
    " GVim
    let g:indentLine_color_gui = '#586e75'
    " none X terminal
    let g:indentLine_color_tty_light = 7 " (default: 4)
    let g:indentLine_color_dark = 1 " (default: 2)
    " Vim & GVim
    let g:indentLine_char = '│' " ¦,┆,│
    " disable by default
    let g:indentLine_enabled = 0
    map <silent> <Leader>i :<C-U>IndentLinesToggle<CR>
endif

if s:has_bundle('vim-indent-guides')
    let g:indent_guides_enable_on_vim_startup = 1
    let g:indent_guides_exclude_filetypes = ['help', 'nerdtree']
    let g:indent_guides_start_level = 2
    let g:indent_guides_auto_colors = 0
    au VimEnter,Colorscheme * :hi IndentGuidesOdd  guibg=#002B36 ctermbg=8
    au VimEnter,Colorscheme * :hi IndentGuidesEven guibg=#073642 ctermbg=0
endif

if s:has_bundle('vim-signature')
    let g:SignatureMarkTextHLDynamic = 1
    let g:SignatureMarkerTextHLDynamic = 1
endif

if s:has_bundle('nerdtree')
    map <C-e> <plug>NERDTreeTabsToggle<CR>
    map <leader>e :NERDTreeFind<CR>

    let NERDTreeShowBookmarks = 1
    let NERDTreeIgnore = [
                \ '\.py[cd]$', '\~$', '\.swo$', '\.swp$', '^\.git$',
                \ '^\.hg$', '^\.svn$', '\.bzr$',
                \ ]
    let NERDTreeChDirMode = 0
    let NERDTreeQuitOnOpen = 1
    let NERDTreeMouseMode = 2
    let NERDTreeShowHidden = 1
    let NERDTreeKeepTreeInNewTab = 1
    let NERDTreeBookmarksFile = $HOME . '/.vim/NERDTreeBookmarks'
    let g:NERDShutUp = 1
    let g:nerdtree_tabs_open_on_gui_startup = 0
endif

if s:has_bundle('undotree')
    nnoremap <Leader>u :UndotreeToggle<CR>
    " If undotree is opened, it is likely one wants to interact with it.
    let g:undotree_SetFocusWhenToggle = 1
endif

if s:has_bundle('ctrlp.vim')
    let g:ctrlp_working_path_mode = 'ra'
    let g:ctrlp_extensions = ['quickfix']
    let g:ctrlp_custom_ignore = {
                \ 'dir':  '\.git$\|\.hg$\|\.svn$',
                \ 'file': '\.exe$\|\.so$\|\.dll$\|\.pyc$' }

    nnoremap <silent> <Leader>m :CtrlPMRU<CR>

    " On Windows use "dir" as fallback command.
    if WINDOWS()
        let s:ctrlp_fallback = 'dir %s /-n /b /s /a-d'
    elseif executable('ag')
        let s:ctrlp_fallback = 'ag %s --nocolor -l -g ""'
    elseif executable('ack-grep')
        let s:ctrlp_fallback = 'ack-grep %s --nocolor -f'
    elseif executable('ack')
        let s:ctrlp_fallback = 'ack %s --nocolor -f'
    else
        let s:ctrlp_fallback = 'find %s -type f'
    endif
    let g:ctrlp_user_command = {
                \ 'types': {
                \     1: ['.git', 'cd %s && git ls-files . --cached --exclude-standard --others'],
                \     2: ['.hg', 'hg --cwd %s locate -I .'],
                \ },
                \ 'fallback': s:ctrlp_fallback
                \ }

    if s:has_bundle('ctrlp-funky')
        " CtrlP extensions
        let g:ctrlp_extensions = ['funky']

        "funky
        nnoremap <Leader>fu :CtrlPFunky<Cr>
    endif

endif

if s:has_bundle('ctrlsf.vim')
    let g:ctrlsf_auto_close = 0
    let g:ctrlsf_default_root = 'project'
    nmap     <C-F>f <Plug>CtrlSFPrompt
    vmap     <C-F>f <Plug>CtrlSFVwordPath
    vmap     <C-F>F <Plug>CtrlSFVwordExec
    nmap     <C-F>n <Plug>CtrlSFCwordPath
    nmap     <C-F>p <Plug>CtrlSFPwordPath
    nnoremap <C-F>o :CtrlSFOpen<CR>
    nnoremap <C-F>t :CtrlSFToggle<CR>
    inoremap <C-F>t <Esc>:CtrlSFToggle<CR>
endif

if s:has_bundle('sessionman.vim')
    nmap <leader>sl :SessionList<CR>
    nmap <leader>ss :SessionSave<CR>
    nmap <leader>sc :SessionClose<CR>
endif

if s:has_bundle('vim-multiple-cursors')
    vmap // :MultipleCursorsFind<space>
endif

if s:has_bundle('wildfire.vim')
    nmap <leader>s <Plug>(wildfire-quick-select)
    let g:wildfire_objects = {
                \ "*" : split("i' i\" i) i] i} ip"),
                \ "json" : split("i\" a\" i] a] a, i} a} a, ip"),
                \ "html,xml" : split("i' i\" i) i] i} at it"),
                \ "python" : split("i' i\" i) i] i} ip if ic"),
                \ "text,textile,markdown" : split("iQ iq is i) i} ip"),
                \ }
endif

if s:has_bundle('vim-textobj-sentence')
    augroup textobj_sentence
        au!
        au FileType markdown call textobj#sentence#init()
        au FileType textile  call textobj#sentence#init()
        au FileType text     call textobj#sentence#init()
    augroup END
endif

if s:has_bundle('vim-textobj-quote')
    augroup textobj_quote
        au!
        au FileType markdown call textobj#quote#init()
        au FileType textile  call textobj#quote#init()
        au FileType text     call textobj#quote#init({'educate': 0})
    augroup END
endif

if s:has_bundle('syntastic')
    let g:syntastic_cpp_compiler_options = ' -std=c++11'
endif

if s:has_bundle('vim-template')
    let g:username = s:your_name
    let g:email = s:your_email
    let g:license = s:your_default_license
    let g:templates_no_builtin_templates = 1
    let g:templates_directory = ['~/.vim/templates/']
endif

if s:has_bundle('vim-autoformat')
    nnoremap <Leader>f :Autoformat<CR>
    " let g:formatdef_my_clangformat = "'clang-format -lines='.a:firstline.':'.a:lastline.' --assume-filename='.bufname('%').' -style=\"{BasedOnStyle: WebKit, AlignTrailingComments: true, '.(&textwidth ? 'ColumnLimit: '.&textwidth.', ' : '').(&expandtab ? 'UseTab: Never, IndentWidth: '.&shiftwidth : 'UseTab: Always').'}\"'"
    let g:formatdef_my_astyle_c = '"astyle --mode=c --style=java -pcH".(&expandtab ? "s".&shiftwidth : "t")'
    let g:formatters_c = ['my_astyle_c']
    let g:formatdef_my_astyle_cpp = '"astyle --mode=c --style=java -pcH".(&expandtab ? "s".&shiftwidth : "t")'
    let g:formatters_cpp = ['my_astyle_cpp']
endif

if s:has_bundle('tabular')
    nmap <Leader>a      :Tabularize /
    vmap <Leader>a      :Tabularize /
    nmap <Leader>a&     :Tabularize /&<CR>
    vmap <Leader>a&     :Tabularize /&<CR>
    nmap <Leader>a=     :Tabularize /=<CR>
    vmap <Leader>a=     :Tabularize /=<CR>
    nmap <Leader>a=>    :Tabularize /=><CR>
    vmap <Leader>a=>    :Tabularize /=><CR>
    nmap <Leader>a:     :Tabularize /:<CR>
    vmap <Leader>a:     :Tabularize /:<CR>
    nmap <Leader>a::    :Tabularize /:\zs<CR>
    vmap <Leader>a::    :Tabularize /:\zs<CR>
    nmap <Leader>a,     :Tabularize /,<CR>
    vmap <Leader>a,     :Tabularize /,<CR>
    nmap <Leader>a,,    :Tabularize /,\zs<CR>
    vmap <Leader>a,,    :Tabularize /,\zs<CR>
    nmap <Leader>a'     :Tabularize /'<CR>
    vmap <Leader>a'     :Tabularize /'<CR>
    nmap <Leader>a''    :Tabularize /'\zs<CR>
    vmap <Leader>a''    :Tabularize /'\zs<CR>
    nmap <Leader>a"     :Tabularize /"<CR>
    vmap <Leader>a"     :Tabularize /"<CR>
    nmap <Leader>a<Bar> :Tabularize /<Bar><CR>
    vmap <Leader>a<Bar> :Tabularize /<Bar><CR>
endif

if s:has_bundle('tagbar')
    nnoremap <silent> <leader>tt :TagbarToggle<CR>
    " If using go please install the gotags program using the following
    " go install github.com/jstemmer/gotags
    " And make sure gotags is in your path
    let g:tagbar_type_go = {
                \ 'ctagstype' : 'go',
                \ 'kinds'     : [
                \       'p:package', 'i:imports:1', 'c:constants', 'v:variables',
                \       't:types',  'n:interfaces', 'w:fields', 'e:embedded',
                \       'm:methods', 'r:constructor', 'f:functions',
                \  ],
                \ 'sro' : '.',
                \ 'kind2scope' : { 't' : 'ctype', 'n' : 'ntype' },
                \ 'scope2kind' : { 'ctype' : 't', 'ntype' : 'n' },
                \ 'ctagsbin'   : 'gotags',
                \ 'ctagsargs'  : '-sort -silent'
                \ }
endif

if s:has_bundle('vim-fugitive')
    nnoremap <silent> <leader>gs :Gstatus<CR>
    nnoremap <silent> <leader>gd :Gdiff<CR>
    nnoremap <silent> <leader>gc :Gcommit<CR>
    nnoremap <silent> <leader>gb :Gblame<CR>
    nnoremap <silent> <leader>gl :Glog<CR>
    nnoremap <silent> <leader>gp :Git push<CR>
    nnoremap <silent> <leader>gr :Gread<CR>
    nnoremap <silent> <leader>gw :Gwrite<CR>
    nnoremap <silent> <leader>ge :Gedit<CR>
    nnoremap <silent> <leader>gi :Git add -p %<CR>
    nnoremap <silent> <leader>gg :SignifyToggle<CR>
endif

if exists("+omnifunc")
    hi Pmenu      guifg=#000000 guibg=#F8F8F8          ctermfg=black     ctermbg=Lightgray
    hi PmenuSbar  guifg=#8A95A7 guibg=#F8F8F8 gui=NONE ctermfg=darkcyan  ctermbg=lightgray cterm=NONE
    hi PmenuThumb guifg=#F8F8F8 guibg=#8A95A7 gui=NONE ctermfg=lightgray ctermbg=darkcyan  cterm=NONE
    " Some convenient mappings
    inoremap <expr> <Esc>  pumvisible() ? "\<C-e>" : "\<Esc>"
    inoremap <expr> <CR>   pumvisible() ? "\<C-y>" : "\<CR>"
    inoremap <expr> <Down> pumvisible() ? "\<C-n>" : "\<Down>"
    inoremap <expr> <Up>   pumvisible() ? "\<C-p>" : "\<Up>"
    inoremap <expr> <C-d>  pumvisible() ? "\<PageDown>\<C-p>\<C-n>" : "\<C-d>"
    inoremap <expr> <C-u>  pumvisible() ? "\<PageUp>\<C-p>\<C-n>" : "\<C-u>"
    au Filetype *
                \  if &omnifunc == ""
                \|     setlocal omnifunc=syntaxcomplete#Complete
                \| endif
    " Automatically open and close the popup menu / preview window
    au CursorMovedI,InsertLeave *
                \  if pumvisible() == 0
                \|     silent! pclose
                \| endif
    set completeopt=menu,preview,longest
endif

if s:completer == 'snipmate' && s:has_bundle('vim-snipmate')
    " Setting the author var
    let g:snips_author = s:your_name . ' <' . s:your_email . '>'

elseif s:completer == 'youcompleteme' && s:has_bundle('YouCompleteMe')
    let g:acp_enableAtStartup = 0

    " enable completion from tags
    let g:ycm_collect_identifiers_from_tags_files = 1
    let g:ycm_global_ycm_extra_conf = '~/.vim/.ycm_extra_conf.py'

    " remap Ultisnips for compatibility for YCM
    let g:UltiSnipsExpandTrigger = '<C-j>'
    let g:UltiSnipsJumpForwardTrigger = '<C-j>'
    let g:UltiSnipsJumpBackwardTrigger = '<C-k>'

    " Enable omni completion.
    au FileType css           setlocal omnifunc=csscomplete#CompleteCSS
    au FileType html,markdown setlocal omnifunc=htmlcomplete#CompleteTags
    au FileType javascript    setlocal omnifunc=javascriptcomplete#CompleteJS
    " au FileType python        setlocal omnifunc=pythoncomplete#Complete
    au FileType xml           setlocal omnifunc=xmlcomplete#CompleteTags
    au FileType ruby          setlocal omnifunc=rubycomplete#Complete
    au FileType haskell       setlocal omnifunc=necoghc#omnifunc

    " For snippet_complete marker.
    if has('conceal')
        set conceallevel=2 concealcursor=i
    endif

    " Disable the neosnippet preview candidate window
    " When enabled, there can be too much visual noise
    " especially when splits are used.
    set completeopt-=preview

elseif s:completer == 'neocomplete' && s:has_bundle('neocomplete.vim')
    let g:acp_enableAtStartup = 0
    let g:neocomplete#enable_at_startup = 1
    let g:neocomplete#enable_smart_case = 1
    let g:neocomplete#enable_auto_delimiter = 1
    let g:neocomplete#max_list = 15
    let g:neocomplete#force_overwrite_completefunc = 1

    " Define dictionary.
    let g:neocomplete#sources#dictionary#dictionaries = {
                \ 'default' : '',
                \ 'vimshell' : $HOME . '/.vimshell_hist',
                \ 'scheme' : $HOME . '/.gosh_completions'
                \ }

    " Define keyword.
    if !exists('g:neocomplete#keyword_patterns')
        let g:neocomplete#keyword_patterns = {}
    endif
    let g:neocomplete#keyword_patterns['default'] = '\h\w*'

    " These two lines conflict with the default digraph mapping of <C-K>
    imap <C-k> <Plug>(neosnippet_expand_or_jump)
    smap <C-k> <Plug>(neosnippet_expand_or_jump)

    " <C-k> Complete Snippet
    " <C-k> Jump to next snippet point
    imap <silent><expr><C-k> neosnippet#expandable() ?
                \ "\<Plug>(neosnippet_expand_or_jump)" : (pumvisible() ?
                \ "\<C-e>" : "\<Plug>(neosnippet_expand_or_jump)")

    inoremap <expr><C-g> neocomplete#undo_completion()
    inoremap <expr><C-l> neocomplete#complete_common_string()

    " <C-h>, <BS>: close popup and delete backword char.
    inoremap <expr><BS>  neocomplete#close_popup()."\<C-h>"
    inoremap <expr><C-h> neocomplete#close_popup()."\<C-h>"
    inoremap <expr><C-y> neocomplete#close_popup()
    inoremap <expr><C-e> neocomplete#cancel_popup()

    func! CleverCr()
        if pumvisible()
            if neosnippet#expandable()
                let exp = "\<Plug>(neosnippet_expand)"
                return exp . neocomplete#close_popup()
            else
                return neocomplete#close_popup()
            endif
        else
            return "\<CR>"
        endif
    endfunc

    " <CR> close popup and save indent or expand snippet
    imap <expr> <CR> CleverCr()

    " <CR>: close popup
    " inoremap <expr><CR> pumvisible() ? neocomplete#close_popup() : "\<CR>"
    " <s-CR>: close popup and save indent.
    inoremap <expr><s-CR> pumvisible() ? neocomplete#close_popup()"\<CR>" : "\<CR>"

    " Courtesy of Matteo Cavalleri
    func! CleverTab()
        if pumvisible()
            return "\<C-n>"
        endif
        let substr = strpart(getline('.'), 0, col('.') - 1)
        let substr = matchstr(substr, '[^ \t]*$')
        if strlen(substr) == 0
            " nothing to match on empty string
            return "\<Tab>"
        else
            " existing text matching
            if neosnippet#expandable_or_jumpable()
                return "\<Plug>(neosnippet_expand_or_jump)"
            else
                return neocomplete#start_manual_complete()
            endif
        endif
    endfunc

    " imap <expr> <Tab> CleverTab()

    inoremap <expr><TAB> pumvisible() ? "\<C-n>" : "\<TAB>"
    inoremap <expr><S-TAB> pumvisible() ? "\<C-p>" : "\<TAB>"
    smap <TAB> <Right><Plug>(neosnippet_jump_or_expand)

    " Enable omni completion.
    au FileType css           setlocal omnifunc=csscomplete#CompleteCSS
    au FileType html,markdown setlocal omnifunc=htmlcomplete#CompleteTags
    au FileType javascript    setlocal omnifunc=javascriptcomplete#CompleteJS
    au FileType python        setlocal omnifunc=pythoncomplete#Complete
    au FileType xml           setlocal omnifunc=xmlcomplete#CompleteTags
    au FileType ruby          setlocal omnifunc=rubycomplete#Complete
    au FileType haskell       setlocal omnifunc=necoghc#omnifunc

    " Enable heavy omni completion.
    if !exists('g:neocomplete#sources#omni#input_patterns')
        let g:neocomplete#sources#omni#input_patterns = {}
    endif
    let g:neocomplete#sources#omni#input_patterns.php = '[^. \t]->\h\w*\|\h\w*::'
    let g:neocomplete#sources#omni#input_patterns.perl = '\h\w*->\h\w*\|\h\w*::'
    let g:neocomplete#sources#omni#input_patterns.c = '[^.[:digit:] *\t]\%(\.\|->\)'
    let g:neocomplete#sources#omni#input_patterns.cpp = '[^.[:digit:] *\t]\%(\.\|->\)\|\h\w*::'
    let g:neocomplete#sources#omni#input_patterns.ruby = '[^. *\t]\.\h\w*\|\h\w*::'

elseif s:completer == 'neocomplcache' && s:has_bundle('neocomplcache.vim')
    let g:acp_enableAtStartup = 0
    let g:neocomplcache_enable_at_startup = 1
    let g:neocomplcache_enable_camel_case_completion = 1
    let g:neocomplcache_enable_smart_case = 1
    let g:neocomplcache_enable_underbar_completion = 1
    let g:neocomplcache_enable_auto_delimiter = 1
    let g:neocomplcache_max_list = 15
    let g:neocomplcache_force_overwrite_completefunc = 1

    " Define dictionary.
    let g:neocomplcache_dictionary_filetype_lists = {
                \ 'default' : '',
                \ 'vimshell' : $HOME . '/.vimshell_hist',
                \ 'scheme' : $HOME . '/.gosh_completions'
                \ }

    " Define keyword.
    if !exists('g:neocomplcache_keyword_patterns')
        let g:neocomplcache_keyword_patterns = {}
    endif
    let g:neocomplcache_keyword_patterns._ = '\h\w*'

    " These two lines conflict with the default digraph mapping of <C-K>
    imap <C-k> <Plug>(neosnippet_expand_or_jump)
    smap <C-k> <Plug>(neosnippet_expand_or_jump)
    imap <silent><expr><C-k> neosnippet#expandable_or_jumpable() ?
                \ "\<Plug>(neosnippet_expand_or_jump)" : (pumvisible() ?
                \ "\<C-e>" : "\<Plug>(neosnippet_expand_or_jump)")

    inoremap <expr><C-g> neocomplcache#undo_completion()
    inoremap <expr><C-l> neocomplcache#complete_common_string()
    "inoremap <expr><CR> neocomplcache#complete_common_string()

    " <C-h>, <BS>: close popup and delete backword char.
    inoremap <expr><BS> neocomplcache#smart_close_popup()."\<C-h>"
    inoremap <expr><C-y> neocomplcache#close_popup()

    func! CleverCr()
        if pumvisible()
            if neosnippet#expandable()
                echo "expandable"
                let exp = "\<Plug>(neosnippet_expand)"
                return exp . neocomplcache#close_popup()
            else
                return neocomplcache#close_popup()
            endif
        else
            return "\<CR>"
        endif
    endfunc

    " <CR> close popup and save indent or expand snippet
    imap <expr> <CR> CleverCr()

    " <CR>: close popup
    " inoremap <expr><CR> pumvisible() ? neocomplcache#close_popup() : "\<CR>"
    " <s-CR>: close popup and save indent.
    inoremap <expr><s-CR> pumvisible() ? neocomplcache#close_popup()"\<CR>" : "\<CR>"

    " <TAB>: completion.
    inoremap <expr><TAB> pumvisible() ? "\<C-n>" : "\<TAB>"
    inoremap <expr><S-TAB> pumvisible() ? "\<C-p>" : "\<TAB>"
    smap <TAB> <Right><Plug>(neosnippet_jump_or_expand)

    " Enable omni completion.
    au FileType css           setlocal omnifunc=csscomplete#CompleteCSS
    au FileType html,markdown setlocal omnifunc=htmlcomplete#CompleteTags
    au FileType javascript    setlocal omnifunc=javascriptcomplete#CompleteJS
    au FileType python        setlocal omnifunc=pythoncomplete#Complete
    au FileType xml           setlocal omnifunc=xmlcomplete#CompleteTags
    au FileType ruby          setlocal omnifunc=rubycomplete#Complete
    au FileType haskell       setlocal omnifunc=necoghc#omnifunc

    " Enable heavy omni completion.
    if !exists('g:neocomplcache_omni_patterns')
        let g:neocomplcache_omni_patterns = {}
    endif
    let g:neocomplcache_omni_patterns.php = '[^. \t]->\h\w*\|\h\w*::'
    let g:neocomplcache_omni_patterns.perl = '\h\w*->\h\w*\|\h\w*::'
    let g:neocomplcache_omni_patterns.c = '[^.[:digit:] *\t]\%(\.\|->\)'
    let g:neocomplcache_omni_patterns.cpp = '[^.[:digit:] *\t]\%(\.\|->\)\|\h\w*::'
    let g:neocomplcache_omni_patterns.ruby = '[^. *\t]\.\h\w*\|\h\w*::'
    let g:neocomplcache_omni_patterns.go = '\h\w*\.\?'

else
    " Enable omni-completion.
    au FileType css           setlocal omnifunc=csscomplete#CompleteCSS
    au FileType html,markdown setlocal omnifunc=htmlcomplete#CompleteTags
    au FileType javascript    setlocal omnifunc=javascriptcomplete#CompleteJS
    au FileType python        setlocal omnifunc=pythoncomplete#Complete
    au FileType xml           setlocal omnifunc=xmlcomplete#CompleteTags
    au FileType ruby          setlocal omnifunc=rubycomplete#Complete
    au FileType haskell       setlocal omnifunc=necoghc#omnifunc
endif

if s:has_bundle('neosnippet')
    " Use honza's snippets.
    let g:neosnippet#snippets_directory = '~/.vim/bundle/vim-snippets/snippets'

    " Enable neosnippet snipmate compatibility mode
    let g:neosnippet#enable_snipmate_compatibility = 1

    " For snippet_complete marker.
    if has('conceal')
        set conceallevel=2 concealcursor=i
    endif

    " Enable neosnippets when using go
    let g:go_snippet_engine = "neosnippet"

    " Disable the neosnippet preview candidate window
    " When enabled, there can be too much visual noise
    " especially when splits are used.
    set completeopt-=preview
endif

if s:has_bundle('python-mode')
    " Disable if python support not present
    if !has('python')
        let g:pymode = 0
    endif

    let g:pymode_doc_bind = 'K'
    let g:pymode_lint_checkers = ['pyflakes', 'pep8', 'mccabe']
    let g:pymode_lint_cwindow = 0
    let g:pymode_lint_options_pep8 =
        \ {'max_line_length': 100}
    let g:pymode_trim_whitespaces = 0
    let g:pymode_options = 0
    let g:pymode_rope = 0
    let g:pymode_rope_completion = 0
    let g:pymode_rope_goto_definition_bind = "<C-]>"
endif

if s:has_bundle('emmet-vim')
    let g:user_emmet_install_global = 0
    let g:user_emmet_install_global = 0
    autocmd FileType html,css EmmetInstall
endif

if s:has_bundle('vim-json')
    nmap <leader>jt <Esc>:%!python -m json.tool<CR><Esc>:set filetype=json<CR>
    let g:vim_json_syntax_conceal = 0
endif

if s:has_bundle('HTML-AutoCloseTag')
    " Make it so AutoCloseTag works for xml and xhtml files as well
    au FileType xhtml,xml ru ftplugin/html/autoclosetag.vim
    nmap <Leader>ac <Plug>ToggleAutoCloseMappings
endif

if s:has_bundle('vim2hs')
    let g:hpaste_author = s:your_name
endif

if s:has_bundle('haskellmode-vim')
    let g:haddock_browser = "open"
    let g:haddock_docdir  = "/usr/share/doc/ghc/html/"
endif

if s:has_bundle('vim-renamer')
    let g:RenamerSupportColonWToRename = 1
endif

